defaultTasks 'clean', 'build'

group = 'com.insightfullogic'

subprojects {
    apply plugin: 'java'

    compileJava {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
    compileJava.options.encoding = 'UTF-8'

    repositories {
        mavenCentral()
        mavenLocal()
    }

    dependencies {
        compile group: 'com.sparkjava', name: 'spark-core', version: '1.1.1'
        compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.3.2'

        testCompile group: 'org.apache.httpcomponents', name: 'fluent-hc', version: '4.3.2'
        testCompile group: 'junit', name: 'junit', version: '4.11'
    }

    test {
        testLogging {
            events 'passed', 'skipped', 'failed'
            showStandardStreams = true
            exceptionFormat = 'full'
            afterSuite { desc, result ->
                if (!desc.parent) {
                    println "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
                }
            }
        }
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task testJar(type: Jar, dependsOn: testClasses) {
        baseName = "test-${project.archivesBaseName}"
        from sourceSets.test.output
    }

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    configurations {
        tests
    }

    artifacts {
        archives sourcesJar, javadocJar
        tests testJar
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.10'
}
